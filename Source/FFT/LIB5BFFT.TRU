1000 EXTERNAL     !LIB5BFFT.TRU
1010
1020
7360 !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
7370 SUB HJW( N, NU, Q, R(), MATK(,), XRE(), XIM(), X2RE(), X2IM(), FFTRE(), FFTIM(), FFT2RE(), FFT2IM(), IFFTRE(), IFFTIM(), TEMPRE(), TEMPIM(), TEMP2RE(), TEMP2IM(), IBR, KOS(), ZIN(), XFLAG, X2FLAG, REALFLAGX, FFTFLAG, FFT2FLAG, SYMMFLAG, IFFTFLAG, CONVFLAG, FORM$, TYPE$, T, MODE$, NOCONV, MMENU, HJWFLAG, CTOP(), CBOT(), ROM(), COSMULT(), SINMULT(), A$, CALL$, FIRSTTIME, FREEMEM, ORG$, ORG2$, LOOPROUND, KLEEN$ )
7380
7390 LET FIRSTTIME = 1
7400
7410   DO
7420     LET SKIP1 = 0
7430     LET SKIP2 = 0
7440
7450     CALL BOX38( Z1$, A$ )   !CREATE, CHANGE, DISPLAY, LOAD, MAIN MENU
7460
7470     IF Z1$ = "Q" OR Z1$ = "M" THEN
7480       IF Z1$ = "M" THEN
7490         IF HJWFLAG = 0 AND CREATED = 1 THEN
7500           SOUND 500,.1
7510
7520           CALL BOX112( RVO$, A$ )
7530
7540           IF RVO$ = "H" THEN
7550             LET MMENU = 0
7560             LET CHANGEPARAMS = 0
7570           ELSE IF RVO$ = "M" THEN
7580             LET MMENU = 1
7590           END IF
7600         ELSE
7610           LET MMENU = 1
7620         END IF
7630       ELSE IF Z1$ = "Q" THEN
7640         LET LOOPROUND = 1
7650       END IF
7660     ELSE
7670       LET FIRSTTIME = 0
7680       IF Z1$ = "CR" THEN
7690
7700         CALL CREATEHJW
7710
7720       ELSE IF Z1$ = "CH" THEN
7730
7740         CALL CHANGEHJW
7750
7760       ELSE IF Z1$ = "D" THEN    !DISPLAY COEFFICIENTS
7770
7780         CALL SHOWHJW
7790
7800       ELSE IF Z1$ = "L" THEN
7810
7820         CALL LOADHJW
7830
7840       END IF
7850     END IF
7860   LOOP UNTIL MMENU = 1 OR LOOPROUND = 1
7870
7880 !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
7890 SUB CREATEHJW
7900
7910   LET BACKOUT = 0
7920   LET SKIP2 = 0
7930   LET HJWFLAG = 0
7940   CALL CLEARFFT2( FFT2RE(), FFT2IM(), FFT2FLAG )
7950
7960   CLEAR
7970   PRINT TAB(2,1);"THE FREQUENCY RESPONSE THAT YOU ARE ABOUT TO ENTER IS IN THE FORM OF A "
7980   PRINT TAB(3,1);"QUOTIENT OF TWO POLYNOMIALS IN  jw"
7990
8000   PRINT TAB(5,1);"          A0 + A1(jw) + A2(jw)^2 + ... + Ar(jw)^R  <------ "
8010   PRINT TAB(6,1);"  H(jw) = ---------------------------------------"
8020   PRINT TAB(7,1);"          B0 + B1(jw) + B2(jw)^2 + ... + Bs(jw)^S  <------ "
8030
8040   PRINT TAB(9,1);"THE DEGREE OF THE NUMERATOR IS  R  AND OF THE DENOMINATOR  S"
8050
8060   PRINT TAB(12,1);"WHEN YOU EXECUTE  ""LOAD H(jw)""  VALUES WILL BE LOADED INTO  F2"
8070
8080   LET STRING1$ = "WHAT IS THE DEGREE OF THE NUMERATOR (0 <= R <= 16) ? "
8090   DO
8100     CALL TEXTVALUEQ( STRING1$, FDG, FLAG, 15, 1, A$ )
8110     IF FLAG = 1 THEN
8120       LET SKIP2 = 1
8130       EXIT DO
8140     ELSE IF FLAG = 0 THEN
8150       LET DEGTOP = FDG
8160       IF DEGTOP <> INT(DEGTOP) OR DEGTOP < 0 OR DEGTOP > 16 THEN SOUND 500,.1
8170     END IF
8180   LOOP UNTIL FLAG = 1 OR (DEGTOP = INT(DEGTOP) AND DEGTOP >= 0 AND DEGTOP <= 16)
8190
8200   IF SKIP2 = 0 THEN
8210     LET CTOP(-1) = DEGTOP
8220     LET STRING2$ = "WHAT IS THE DEGREE OF THE DENOMINATOR (" & STR$(DEGTOP) & " <= S <= 16) ? "
8230     DO
8240       CALL TEXTVALUEQ( STRING2$, FDG, FLAG, 17, 1, A$ )
8250       IF FLAG = 1 THEN
8260         LET SKIP2 = 1
8270         LET CBOT(-1) = -1
8280         EXIT DO
8290       ELSE IF FLAG = 0 THEN
8300         LET DEGBOT = FDG
8310         IF (DEGBOT <> INT(DEGBOT) OR DEGBOT < DEGTOP OR DEGBOT > 16) THEN SOUND 500,.1
8320       END IF
8330     LOOP UNTIL FLAG = 1 OR (DEGBOT = INT(DEGBOT) AND DEGBOT >= DEGTOP AND DEGBOT <= 16)
8340   END IF
8350
8360   IF SKIP2 = 0 THEN
8370     LET CBOT(-1) = DEGBOT
8380     CLEAR
8390     PRINT TAB(2,1);"YOU ARE NOW CREATING THE NUMERATOR POLYNOMIAL (TOP) OF H(jw)."
8400     FOR I = 0 TO CTOP(-1)
8410       LET STR3$ = "COEFF OF (jw)^" & STR$(I) & " : " & "A(" & STR$(I) & ") = "
8420
8430       CALL TEXTVALUEQ( STR3$, FDG, FLAG, 4+I, 1, A$ )
8440
8450       IF FLAG = 1 THEN
8460         MAT CTOP = 0
8470         LET BACKOUT = 1
8480         EXIT FOR
8490       ELSE IF FLAG = 0 THEN
8500         LET CTOP(I) = FDG
8510       END IF
8520     NEXT I
8530
8540     IF BACKOUT = 0 THEN
8550       CLEAR
8560       FOR I = 0 TO CBOT(-1)
8570         DO
8580           LET LOOPNOW = 0
8590           DO
8600             PRINT TAB(2,1);"YOU ARE NOW CREATING THE DENOMINATOR POLYNOMIAL (BOTTOM) OF H(jw)."
8610             PRINT TAB(4,1);"(ONLY NON-NEGATIVE COEFFICIENTS ACCEPTED)"
8620             IF PAINTBACK = 1 THEN
8630               LET PAINTBACK = 0
8640               PRINT
8650               FOR K = 0 TO CBOT(-1) - 1
8660                 PRINT "COEFF OF (jw)^" & STR$(K) & " : " & "B(" & STR$(K) & ") = " & STR$(CBOT(K))
8670               NEXT K
8680             END IF
8690             LET STR4$ = "COEFF OF (jw)^" & STR$(I) & " : " & "B(" & STR$(I) & ") = "
8700
8710             CALL TEXTVALUEQ( STR4$, FDG, FLAG, 6+I, 1, A$ )
8720
8730             IF FLAG = 1 THEN
8740               MAT CBOT = 0
8750               LET CBOT(-1) = -1
8760               LET BACKOUT = 1
8770               EXIT DO
8780             ELSE IF FLAG = 0 THEN
8790               LET VALUE = FDG
8800               IF I = 0 AND VALUE = 0 THEN
8810
8820                 CALL BOX114B( A$ )      !WON'T ACCEPT ZERO FOR  B(0)
8830
8840                 CLEAR
8850                 LET LOOPNOW = 1
8860               ELSE IF I = CBOT(-1)  AND VALUE = 0 THEN
8870
8880                 CALL BOX114C( CBOT(), A$ )      !WON'T ACCEPT ZERO FOR  B(S)
8890
8900                 CLEAR
8910                 LET PAINTBACK = 1
8920                 LET LOOPNOW = 1
8930               ELSE IF VALUE < 0 THEN
8940                 SOUND 500,.1
8950               END IF
8960             END IF
8970             IF LOOPNOW = 1 THEN EXIT DO
8980           LOOP UNTIL VALUE >= 0
8990         LOOP WHILE LOOPNOW = 1
9000
9010         IF BACKOUT = 1 THEN EXIT FOR
9020
9030         LET CBOT(I) = VALUE
9040       NEXT I
9050
9060       IF BACKOUT = 0 THEN
9070         LET CREATED = 1
9080         CALL SHOWHJW
9090       END IF
9100     END IF
9110   END IF
9120
9130 END SUB !CREATEHJW
9140 !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
9150
9160
9170 !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
9180 SUB SHOWHJW
9190
9200   IF CTOP(-1) = -1 OR CBOT(-1) = -1 THEN
9210     SOUND 500,.1
9220
9230     CALL BOX111
9240
9250   ELSE
9260     CLEAR
9270     PRINT TAB(2,33);"NUMERATOR"
9280     PRINT TAB(2,57);"DENOMINATOR"
9290     IF CTOP(-1) <= 9 THEN
9300       PRINT TAB(3,33);"DEGREE ";CTOP(-1)
9310     ELSE
9320       PRINT TAB(3,33);"DEGREE";CTOP(-1)
9330     END IF
9340     IF CBOT(-1) <= 9 THEN
9350       PRINT TAB(3,58);"DEGREE ";CBOT(-1)
9360     ELSE
9370       PRINT TAB(3,58);"DEGREE";CBOT(-1)
9380     END IF
9390     PRINT TAB(3,9);"COEFF OF"
9400     FOR I = 0 TO MAX(CTOP(-1),CBOT(-1))
9410       LET I$ = STR$(I)
9420       LET STR1$ = "(jw)^" & I$
9430       PRINT TAB(5+I,10);STR1$
9440     NEXT I
9450     FOR I = 0 TO CTOP(-1)
9460       LET I$ = STR$(I)
9470       LET STR1$ = "A(" & I$ & ") ="
9480       PRINT TAB(5+I,33) ; STR1$ ; CTOP(I)
9490     NEXT I
9500     FOR I = 0 TO CBOT(-1)
9510       LET I$ = STR$(I)
9520       LET STR1$ = "B(" & I$ & ") ="
9530       PRINT TAB(5+I,58) ; STR1$ ; CBOT(I)
9540     NEXT I
9550
9560     DO
9570       LET FRAZ$ = "³ CHANGE VALUES ... C ³ LOAD H(jw) ... L ³  ?  "
9580
9590       CALL APROMPT( 23, 18, A$, FRAZ$, ASD$ )
9600
9610       IF ASD$ <> "C" AND ASD$ <> "L" AND ASD$ <> "Q" THEN SOUND 500,.1
9620     LOOP UNTIL ASD$ = "C" OR ASD$ = "L" OR ASD$ = "Q"
9630
9640     IF ASD$ = "C" THEN
9650
9660       CALL CHANGEHJW
9670
9680     ELSE IF ASD$ = "L" THEN
9690
9700       CALL LOADHJW
9710
9720     END IF
9730   END IF
9740 END SUB !SHOWHJW
9750 !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
9760
9770
9780
9790 !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
9800  SUB CHANGEHJW
9810
9820    IF CBOT(-1) = -1 OR CTOP(-1) = -1 THEN
9830      SOUND 500,.1
9840
9850      CALL BOX111
9860
9870    ELSE
9880
9890      CALL BOX113( DUMMY1$, A$ )
9900
9910      LET FLAG = 0
9920      IF DUMMY1$ = "Q" THEN
9930        LET SKIP1 = 1
9940        LET MMENU = 0
9950
9960      ELSE IF DUMMY1$ = "N" OR DUMMY1$ = "D" THEN
9970        LET HJWFLAG = 0
9980
9990        CALL CLEARFFT2( FFT2RE(), FFT2IM(), FFT2FLAG )
10000
10010       DO
10020         IF DUMMY1$ = "N" THEN          !CHANGING NUMERATOR
10030           FOR I = 0 TO CTOP(-1)
10040             LET STRA$ = "A(" & STR$(I) & ")"
10050             LET STRE$ = " = "
10060             DO
10070               LET LOOPA = 0
10080               LET FLAG = 0
10090               DO
10100                 CLEAR
10110                 PRINT TAB(2,1);"NUMERATOR COEFFICIENT:"
10120                 PRINT TAB(2,24); STRA$ & STRE$ & STR$(CTOP(I))   !DISPLAYS CURRENT VALUE
10130                 PRINT TAB(4,1);"³ CHANGE " & STRA$ & " ... C ³"
10140                 PRINT TAB(6,1);"³ GO TO NEXT COEFF ... G ³"
10150                 LET FRAZ$ = "³ SWITCH TO DENOMINATOR ... S ³  ?  "
10160
10170                 CALL APROMPT( 8, 1, A$, FRAZ$, DUMMY2$ )
10180
10190                 IF DUMMY2$ <> "C" AND DUMMY2$ <> "G" AND DUMMY2$ <> "S" AND DUMMY2$ <> "Q" THEN SOUND 500,.1
10200               LOOP UNTIL DUMMY2$ = "C" OR DUMMY2$ = "G" OR DUMMY2$ = "S" OR DUMMY2$ = "Q"
10210
10220               IF DUMMY2$ = "C" THEN              !CHANGING NUMERATOR
10230                 LET STRING$ = "NEW VALUE = ?  "
10240
10250                 CALL TEXTVALUEQ( STRING$, FDG, FLAG, 11, 1, A$)
10260
10270                 IF FLAG = 1 THEN
10280                   LET LOOPA = 1
10290                 ELSE IF FLAG = 0 THEN
10300                   LET CTOP(I) = FDG
10310                   PRINT TAB(13,1);"O.K. NEW VALUE IS " ; CTOP(I)
10320                   CALL BOX48C( A$ )   !  ³ CONTINUE ³
10330                 END IF
10340               END IF
10350             LOOP WHILE LOOPA = 1
10360
10370             IF DUMMY2$ = "G" THEN
10380               PRINT
10390
10400             ELSE IF DUMMY2$ = "S" THEN
10410               LET DUMMY1$ = "D"
10420               EXIT FOR
10430
10440             ELSE IF DUMMY2$ = "Q" THEN
10450               LET SKIP1 = 1
10460               LET MMENU = 0
10470               EXIT FOR
10480
10490             END IF
10500           NEXT I
10510
10520         ELSE IF DUMMY1$ = "D" THEN       !CHANGING DENOMINATOR
10530           FOR I = 0 TO CBOT(-1)
10540             DO
10550               LET FLAG = 0
10560               LET STRB$ = "B(" & STR$(I) & ")"
10570               LET STRE$ = " = "
10580               DO
10590                 CLEAR
10600                 PRINT TAB(2,1);"DENOMINATOR COEFF:"
10610                 PRINT TAB(2,20); STRB$ & STRE$ & STR$(CBOT(I))   !DISPLAYS CURRENT VALUE
10620                 PRINT TAB(4,1);"³ CHANGE " & STRB$ & " ... C ³"
10630                 PRINT TAB(6,1);"³ GO TO NEXT COEFF ... G ³"
10640                 LET FRAZ$ = "³ SWITCH TO NUMERATOR ... S ³  ?  "
10650
10660                 CALL APROMPT( 8, 1, A$, FRAZ$, DUMMY2$ )
10670
10680                 IF DUMMY2$ <> "C" AND DUMMY2$ <> "G" AND DUMMY2$ <> "S" AND DUMMY2$ <> "Q" THEN SOUND 500,.1
10690               LOOP UNTIL DUMMY2$ = "C" OR DUMMY2$ = "G" OR DUMMY2$ = "S" OR DUMMY2$ = "Q"
10700
10710               IF DUMMY2$ = "C" THEN         !CHANGING DENOMINATOR
10720                 DO
10730                   IF I <> 0 AND I <> CBOT(-1) THEN
10740                     LET STRING$ = "NEW VALUE (A NON-NEGATIVE NUMBER) = ?  "
10750                   ELSE IF I = 0 OR I = CBOT(-1) THEN
10760                     LET STRING$ = "NEW VALUE (A POSITIVE NUMBER) = ?  "
10770                   END IF
10780
10790                   CALL TEXTVALUEQ( STRING$, FDG, FLAG, 11, 1, A$ )
10800
10810                   IF FLAG = 1 THEN
10820                     EXIT DO
10830                   ELSE IF FLAG = 0 THEN
10840                     LET VALUE = FDG
10850                     LET TES = 0
10860                     IF I <> 0 AND I <> CBOT(-1) THEN
10870                       IF VALUE >= 0 THEN LET TES = 1
10880                     ELSE IF I = 0 OR I = CBOT(-1) THEN
10890                       IF VALUE > 0 THEN LET TES = 1
10900                     END IF
10910                     IF TES = 1 THEN
10920                       LET CBOT(I) = VALUE
10930                       PRINT
10940                       PRINT TAB(13,1);"O.K. NEW VALUE IS " ; CBOT(I)
10950                       CALL BOX48C( A$ )  ! ³ CONTINUE ³
10960                     ELSE IF TES = 0 THEN
10970                       SOUND 500,.1
10980                     END IF
10990                   END IF
11000                 LOOP UNTIL TES = 1
11010
11020               ELSE IF DUMMY2$ = "G" THEN
11030                 PRINT
11040
11050               ELSE IF DUMMY2$ = "S" THEN
11060                 LET DUMMY1$ = "N"
11070                 EXIT FOR
11080
11090               ELSE IF DUMMY2$ = "Q" THEN
11100                 LET SKIP1 = 1
11110                 LET MMENU = 0
11120                 EXIT FOR
11130               END IF
11140
11150             LOOP WHILE FLAG = 1
11160           NEXT I
11170         END IF
11180       LOOP UNTIL SKIP1 = 1
11190     END IF
11200   END IF
11210
11220 END SUB !CHANGEHJW
11230 !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
11240
11250
11260
11270 !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
11280 SUB LOADHJW
11290
11300   LET BACKOUT = 0
11310   LET SETZERO = 0
11320
11330   IF CTOP(-1) = -1 OR CBOT(-1) = -1 THEN
11340     SOUND 500,.1
11350
11360     CALL BOX111
11370
11380   ELSE
11390     CLEAR
11400     PRINT
11410     LET TESTFLAG = 0
11420
11430     FOR I = 0 TO CBOT(-1)
11440       IF CBOT(I) <> 0 THEN
11450         LET TESTFLAG = 1
11460       END IF
11470     NEXT I
11480
11490     IF 0 = 0 THEN   !ALL O.K.  NOW PROCEEDING TO LOAD  H(jw)
11500
11510       LET HJWFLAG = 0
11520
11530       CALL CLEARFFT2( FFT2RE(), FFT2IM(), FFT2FLAG )
11540
11550       IF CBOT(-1) = CTOP(-1) THEN    !EXTRACTING THE DIRAC DELTA
11560         LET VAL = CTOP(-1)
11570         LET DIRAC = CTOP(VAL)/CBOT(VAL)
11580         FOR K = 0 TO VAL
11590           LET CTOP(K) = CTOP(K) - DIRAC*CBOT(K)
11600         NEXT K
11610       END IF
11620
11630       LET OMEGAs = 2*PI/T
11640       LET P2 = PI/2
11650
11660       DO
11670         LET NEWALPHA = 0
11680         LET SETZER0  = 0
11690
11700         CALL BOX115( BACKOUT, A, A$ )
11710
11720         IF BACKOUT = 1 THEN EXIT DO
11730
11740         IF A >= 64 THEN
11750           LET CC = INT((8 - LOG2(A))/2)
11760           LET DD = 2^CC
11770           LET EE = INT(DD*A)
11780         END IF
11790
11800         CLEAR
11810         PRINT
11820         PRINT "ALIASING LEVEL IS ---> ALPHA = ";A;":  PRESS SPACE BAR TO TAKE CONTROL"
11830         PRINT
11840         LET NUMB = INT(N/32)
11850         IF NUMB = 0 THEN LET NUMB = 1
11860         LET SETZERO = 0
11870         LET J = 0
11880         DO
11890           LET FFT2RE(J) = 0
11900           LET FFT2IM(J) = 0
11910           LET V = J - A*N     !A IS ALIAS LEVEL
11920           IF MOD(J,16) = 0 THEN
11930             PRINT "³"; NUMB; "³";
11940             LET NUMB = NUMB - 1
11950           END IF
11960           DO
11970             IF A >= 64 THEN
11980               LET COUNT1 = COUNT1 + 1
11990               IF MOD(COUNT1,EE) = 0 THEN
12000                 PRINT ".";
12010                 LET COUNT2 = COUNT2 + 1
12020                 IF MOD(COUNT2,1000) = 0 THEN
12030                   PRINT
12040                   PRINT
12050                   PRINT "ALIASING LEVEL IS ---> ALPHA = ";A;":  PRESS SPACE BAR TO TAKE CONTROL"
12060                   PRINT
12070                 END IF
12080               END IF
12090             END IF
12100
12110             IF KEY INPUT THEN
12120               GET KEY INKEY
12130               IF INKEY = 32 THEN
12140
12150                 CALL BOX116( WCT$, A$ )
12160
12170                 CLEAR
12180                 IF WCT$ = "R" THEN
12190                   PRINT
12200                   PRINT "ALIASING LEVEL IS ---> ALPHA = ";A;":  PRESS SPACE BAR TO TAKE CONTROL"
12210                   PRINT
12220                 ELSE IF WCT$ = "N" THEN
12230                   LET SETZERO = 1
12240                   LET NEWALPHA = 1
12250                   MAT FFT2RE = 0
12260                   MAT FFT2IM = 0
12270                 ELSE IF WCT$ = "Q" THEN
12280                   LET SETZERO = 1
12290                 END IF
12300               END IF
12310             END IF
12320
12330             IF SETZERO = 0 THEN
12340               LET RR = V*OMEGAs
12350               FOR K = 0 TO MAX(CTOP(-1),CBOT(-1))
12360                 LET ROM(K) = RR^K
12370                 LET COSMULT(K) = COS(K*P2)    !P2 IS ã/2
12380                 LET SINMULT(K) = SIN(K*P2)
12390               NEXT K
12400
12410               LET TOPRE = 0
12420               LET TOPIM = 0
12430               LET BOTRE = 0
12440               LET BOTIM = 0
12450
12460               FOR K = 0 TO CTOP(-1)
12470                 LET AA = CTOP(K)*ROM(K)
12480                 LET TOPRE = TOPRE + AA*COSMULT(K)
12490                 LET TOPIM = TOPIM + AA*SINMULT(K)
12500               NEXT K
12510               FOR K = 0 TO CBOT(-1)
12520                 LET AA = CBOT(K)*ROM(K)
12530                 LET BOTRE = BOTRE + AA*COSMULT(K)
12540                 LET BOTIM = BOTIM + AA*SINMULT(K)
12550               NEXT K
12560
12570               LET DENOM = BOTRE^2 + BOTIM^2
12580               LET FFT2RE(J) = FFT2RE(J) + (TOPRE*BOTRE + TOPIM*BOTIM)/DENOM
12590               LET FFT2IM(J) = FFT2IM(J) + (TOPIM*BOTRE - TOPRE*BOTIM)/DENOM
12600               LET V = V + N
12610             END IF
12620           LOOP UNTIL V > J + A*N OR SETZERO = 1
12630
12640           LET J = J + 1
12650         LOOP UNTIL J > INT(N/2) OR SETZERO = 1
12660         PRINT !TO RELEASE THE ";"
12670       LOOP WHILE NEWALPHA = 1
12680
12690       IF BACKOUT = 0 THEN
12700         IF SETZERO = 0 THEN
12710           LET SCALE = 1
12720           IF MODE$ = "SAMP" AND TYPE$ = "PER" THEN
12730             LET SCALE = N
12740           ELSE IF MODE$ = "SAMP" AND TYPE$ = "PUL" THEN
12750             LET SCALE = N/T
12760           END IF
12770           LET FFT2IM(0) = 0
12780           IF N/2 = INT(N/2) THEN
12790             LET FFT2IM(N/2) = 0
12800           END IF
12810           FOR K = 0 TO INT(N/2)
12820             LET FFT2RE(K)     = SCALE*(FFT2RE(K) + DIRAC)
12830             LET FFT2RE(N - K) = FFT2RE(K)
12840             LET FFT2IM(K)     = SCALE*FFT2IM(K)
12850             LET FFT2IM(N - K) = -FFT2IM(K)
12860           NEXT K
12870           LET HJWFLAG = 1
12880           LET ORG2$ = "L"
12890           LET MMENU = 1
12900
12910         ELSE IF SETZERO = 1 THEN
12920           MAT FFT2RE = 0
12930           MAT FFT2IM = 0
12940         END IF
12950
12960         IF CBOT(-1) = CTOP(-1) THEN    !RESTORING THE TRANSFER FUNCTION
12970           FOR K = 0 TO VAL
12980             LET CTOP(K) = CTOP(K) + DIRAC*CBOT(K)
12990           NEXT K
13000         END IF
13010
13020         PRINT
13030         CALL TESTFFTDATA( N, FFTRE(), FFTIM(), FFT2RE(), FFT2IM(), TEMP2RE(), TEMP2IM(), FFTFLAG, FFT2FLAG, SYMMFLAG, NOCONV, HJWFLAG, KLEEN$ )
13040         PRINT !TO RELEASE THE ";"
13050       END IF
13060     END IF
13070   END IF
13080 END SUB !LOADHJW
13090 !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
13100
13110 END SUB !Hjw
13120 !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
13130
13140
13150
13160 !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
13170 !SUB LOADHS( N, T, CTOP(), CBOT(), ROM(), COSMULT(), SINMULT(), FFT2RE(), FFT2IM(), MODE$, TYPE$, MMENU, HJWFLAG, A$, SIGMA )
13180 !  CLEAR
13190 !  PRINT
13200 !
13210 !  IF CBOT(-1) = CTOP(-1) THEN    !EXTRACTING THE DIRAC DELTA
13220 !    LET VAL = CTOP(-1)
13230 !    LET DIRAC = CTOP(VAL)/CBOT(VAL)
13240 !    FOR K = 0 TO VAL
13250 !      LET CTOP(K) = CTOP(K) - DIRAC*CBOT(K)
13260 !    NEXT K
13270 !  END IF
13280 !
13290 !  LET OMEGAs = 2*PI/T
13300 !  LET P2 = PI/2
13310 !
13320 !  PRINT "WE ARE NOW LOADING  H(s)  INTO  F2"
13330 !  DO
13340 !    PRINT
13350 !    PRINT "WHAT ALIAS LEVEL (ALPHA) DO YOU WANT"
13360 !    PRINT
13370 !    INPUT PROMPT "A NON-NEGATIVE INTEGER  ?  "  :  A
13380 !    PRINT
13390 !    IF A <> INT(A) OR A < 0 THEN SOUND 500,.1
13400 !  LOOP UNTIL A = INT(A) AND A >= 0
13410 !
13420 !  IF A >= 64 THEN
13430 !    LET CC = INT((8 - LOG2(A))/2)
13440 !    LET DD = 2^CC
13450 !    LET EE = INT(DD*A)
13460 !  END IF
13470 !
13480 !  CLEAR
13490 !  PRINT
13500 !  PRINT "PRESS SPACE BAR TO TAKE CONTROL"
13510 !  PRINT
13520 !  LET NUMB = INT(N/32)
13530 !  IF NUMB = 0 THEN LET NUMB = 1
13540 !  LET SETZERO = 0
13550 !  LET J = 0
13560 !  DO
13570 !    LET FFT2RE(J) = 0
13580 !    LET FFT2IM(J) = 0
13590 !    LET V = J - A*N     !A IS ALIAS LEVEL
13600 !    IF MOD(J,16) = 0 THEN
13610 !      PRINT "³"; NUMB; "³";
13620 !      LET NUMB = NUMB - 1
13630 !    END IF
13640 !    DO
13650 !      IF A >= 64 THEN
13660 !        LET COUNT1 = COUNT1 + 1
13670 !        IF MOD(COUNT1,EE) = 0 THEN
13680 !          PRINT ".";
13690 !          LET COUNT2 = COUNT2 + 1
13700 !          IF MOD(COUNT2,1000) = 0 THEN
13710 !            PRINT
13720 !            PRINT
13730 !            PRINT "PRESS SPACE BAR TO TAKE CONTROL"
13740 !            PRINT
13750 !          END IF
13760 !        END IF
13770 !      END IF
13780 !      IF KEY INPUT THEN
13790 !        GET KEY INKEY
13800 !        IF INKEY = 32 THEN
13810 !          PRINT
13820 !          DO
13830 !            PRINT
13840 !            INPUT PROMPT "³ RESUME CALCULATION ... R ³ QUIT ... Q ³  ?  " : WCT$
13850 !            LET WCT$ = UCASE$(WCT$)
13860 !            IF WCT$ <> "R" AND WCT$ <> "Q" THEN SOUND 500,.1
13870 !          LOOP UNTIL WCT$ = "R" OR WCT$ = "Q"
13880 !          IF WCT$ = "R" THEN
13890 !            PRINT
13900 !            PRINT "PRESS SPACE BAR TO TAKE CONTROL"
13910 !            PRINT
13920 !          ELSE IF WCT$ = "Q" THEN
13930 !            LET SETZERO = 1
13940 !          END IF
13950 !        END IF
13960 !      END IF
13970 !      LET R = V*OMEGAs     !(J - A*N)*OMEGAs
13980 !      LET G = SQR(SIGMA^2 + R^2)
13990 !      IF SIGMA <> 0 OR R <> 0 THEN
14000 !        LET THETA = ANGLE(SIGMA,R)
14010 !      ELSE
14020 !        LET THETA = 0
14030 !      END IF
14040 !      FOR K = 0 TO MAX(CTOP(-1),CBOT(-1))
14050 !        LET ROM(K) = G^K
14060 !        LET COSMULT(K) = COS(K*THETA)
14070 !        LET SINMULT(K) = SIN(K*THETA)
14080 !      NEXT K
14090 !
14100 !      LET TOPRE = 0
14110 !      LET TOPIM = 0
14120 !      LET BOTRE = 0
14130 !      LET BOTIM = 0
14140 !
14150 !      FOR K = 0 TO CTOP(-1)
14160 !        LET AA = CTOP(K)*ROM(K)
14170 !        LET TOPRE = TOPRE + AA*COSMULT(K)
14180 !        LET TOPIM = TOPIM + AA*SINMULT(K)
14190 !      NEXT K
14200 !      FOR K = 0 TO CBOT(-1)
14210 !        LET AA = CBOT(K)*ROM(K)
14220 !        LET BOTRE = BOTRE + AA*COSMULT(K)
14230 !        LET BOTIM = BOTIM + AA*SINMULT(K)
14240 !      NEXT K
14250 !
14260 !      LET DENOM = BOTRE^2 + BOTIM^2
14270 !
14280 !      WHEN ERROR IN
14290 !        LET FFT2RE(J) = FFT2RE(J) + (TOPRE*BOTRE + TOPIM*BOTIM)/DENOM
14300 !        LET FFT2IM(J) = FFT2IM(J) + (TOPIM*BOTRE - TOPRE*BOTIM)/DENOM
14310 !      USE
14320 !        CLEAR
14330 !        PRINT
14340 !        SOUND 500,.1
14350 !        PRINT "YOU ARE ATTEMPTING TO DIVIDE BY ZERO!"
14360 !        PRINT
14370 !        PRINT "YOU MUST GO BACK AND FIX THE PROBLEM."
14380 !        DO
14390 !          PRINT
14400 !          INPUT PROMPT "³ CONTINUE ... C ³  ?  "  :  WCO$
14410 !          LET WCO$ = UCASE$(WCO$)
14420 !        LOOP UNTIL WCO$ = "C"
14430 !        LET SETZERO = 1
14440 !      END WHEN
14450 !      LET V = V + N
14460 !    LOOP UNTIL V > J + A*N OR SETZERO = 1
14470 !
14480 !    LET J = J + 1
14490 !  LOOP UNTIL J > INT(N/2) OR SETZERO = 1
14500 !  PRINT !TO RELEASE THE ";"
14510 !
14520 !  IF SETZERO = 0 THEN
14530 !    LET SCALE = 1
14540 !    IF MODE$ = "SAMP" AND TYPE$ = "PER" THEN
14550 !      LET SCALE = N
14560 !    ELSE IF MODE$ = "SAMP" AND TYPE$ = "PUL" THEN
14570 !      LET SCALE = N/T
14580 !    END IF
14590 !    LET FFT2IM(0) = 0
14600 !    IF N/2 = INT(N/2) THEN
14610 !      LET FFT2IM(N/2) = 0
14620 !    END IF
14630 !    FOR K = 0 TO INT(N/2)
14640 !      LET FFT2RE(K)     = SCALE*(FFT2RE(K) + DIRAC)
14650 !      LET FFT2RE(N - K) = FFT2RE(K)
14660 !      LET FFT2IM(K)     = SCALE*FFT2IM(K)
14670 !      LET FFT2IM(N - K) = -FFT2IM(K)
14680 !    NEXT K
14690 !    LET HJWFLAG = 1
14700 !  ELSE IF SETZERO = 1 THEN
14710 !    MAT FFT2RE = 0
14720 !    MAT FFT2IM = 0
14730 !  END IF
14740 !
14750 !  IF CBOT(-1) = CTOP(-1) THEN    !RESTORING THE TRANSFER FUNCTION
14760 !    FOR K = 0 TO VAL
14770 !      LET CTOP(K) = CTOP(K) + DIRAC*CBOT(K)
14780 !    NEXT K
14790 !  END IF
14800 !
14810 !END SUB !LOADHS
14820 !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
14830
14840
14850
14860 !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
14870 SUB CLEARFFT2( FFT2RE(), FFT2IM(), FFT2FLAG )
14880
14890   MAT FFT2RE = 0
14900   MAT FFT2IM = 0
14910   LET FFT2FLAG = 0
14920
14930 END SUB !CLEARFFT2
14940 !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
