1000 EXTERNAL     !LIB5CFFT.TRU
1010
1020
14980 !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
14990 SUB CONVOLUTION( FORM$, N, NU, Q, R(), TEMPR(), MATK(,), DITK(), TEMPDITK(), XRE(), XIM(), X2RE(), X2IM(), FFTRE(), FFTIM(), FFT2RE(), FFT2IM(), IFFTRE(), IFFTIM(), TEMPRE(), TEMPIM(), TEMP2RE(), TEMP2IM(), KOS(), ZIN(), IBR, XFLAG, X2FLAG, REALFLAGX, FFTFLAG, FFT2FLAG, SYMMFLAG, IFFTFLAG, REALFLAGIFFT, CONVFLAG, T, MODE$, TYPE$, HJWFLAG, NOCONV, A$, KLEEN$, CONCORFLAG )
15000
15010 CALL BOX54( "C", Z0$, A$, CONCORFLAG )
15020
15030 IF Z0$ = "Q" THEN LET MMENU = 1
15040
15050 IF MMENU = 0 AND HJWFLAG = 1 THEN
15060   CLEAR
15070
15080   CALL BOX59( CRT$, A$ )
15090
15100   IF CRT$ = "R" THEN
15110     LET HJWFLAG = 0
15120     CALL CLEARFFT2( FFT2RE(), FFT2IM(), FFT2FLAG )
15130   ELSE IF CRT$ = "Q" THEN
15140     LET MMENU = 1
15150   END IF
15160 END IF
15170
15180 IF MMENU = 0 AND (TYPE$ = "PUL" OR MODE$ = "DISC") THEN
15190   CLEAR
15200   PRINT
15210   FOR K = 0 TO N - 1
15220     LET TEMPRE(K) = ABS(XRE(K))
15230     LET TEMPIM(K) = ABS(XIM(K))
15240     LET A1 = MAX(A1, TEMPRE(K))
15250     LET A2 = MAX(A2, TEMPIM(K))
15260     IF MOD(K,128) = 0 THEN PRINT ".";
15270   NEXT K
15280   IF A1 <> 0 THEN
15290     FOR K = 0 TO N - 1
15300       IF TEMPRE(K) < .01*A1 THEN LET TEMPRE(K) = 0
15310       IF MOD(K,128) = 0 THEN PRINT ".";
15320     NEXT K
15330   END IF
15340   IF A2 <> 0 THEN
15350     FOR K = 0 TO N - 1
15360       IF TEMPIM(K) < .01*A2 THEN LET TEMPIM(K) = 0
15370       IF MOD(K,128) = 0 THEN PRINT ".";
15380     NEXT K
15390   END IF
15400   LET FLAG   = 0
15410   LET COUNT1 = 0
15420   LET COUNT2 = 0
15430   LET COUNT3 = 0
15440   FOR K = 0 TO N - 1
15450     IF TEMPRE(K) = 0 AND TEMPIM(K) = 0 THEN
15460       IF FLAG = 0 THEN
15470         LET COUNT3 = COUNT3 + 1
15480       ELSE IF FLAG = 1 THEN
15490         LET COUNT1 = COUNT1 + 1
15500       END IF
15510     ELSE IF TEMPRE(K) <> 0 OR TEMPIM(K) <> 0 THEN
15520       LET FLAG = 1
15530       LET COUNT2 = MAX(COUNT1, COUNT2)
15540       LET COUNT1 = 0
15550     END IF
15560     IF MOD(K,128) = 0 THEN PRINT ".";
15570   NEXT K
15580   LET COUNT3 = COUNT3 + COUNT1
15590   LET COUNT2 = MAX(COUNT2, COUNT3)
15600   LET PNUM = N - COUNT2
15610   FOR K = 0 TO N - 1
15620     LET TEMP2RE(K) = ABS(X2RE(K))
15630     LET TEMP2IM(K) = ABS(X2IM(K))
15640     LET A3 = MAX(A3, TEMP2RE(K))
15650     LET A4 = MAX(A4, TEMP2IM(K))
15660     IF MOD(K,128) = 0 THEN PRINT ".";
15670   NEXT K
15680   IF A3 <> 0 THEN
15690     FOR K = 0 TO N - 1
15700       IF TEMP2RE(K) < .001*A3 THEN LET TEMP2RE(K) = 0
15710       IF MOD(K,128) = 0 THEN PRINT ".";
15720     NEXT K
15730   END IF
15740   IF A4 <> 0 THEN
15750     FOR K = 0 TO N - 1
15760       IF TEMP2IM(K) < .001*A4 THEN LET TEMP2IM(K) = 0
15770       IF MOD(K,128) = 0 THEN PRINT ".";
15780     NEXT K
15790   END IF
15800   LET FLAG   = 0
15810   LET COUNT1 = 0
15820   LET COUNT2 = 0
15830   LET COUNT3 = 0
15840   FOR K = 0 TO N - 1
15850     IF TEMP2RE(K) = 0 AND TEMP2IM(K) = 0 THEN
15860       IF FLAG = 0 THEN
15870         LET COUNT3 = COUNT3 + 1
15880       ELSE IF FLAG = 1 THEN
15890         LET COUNT1 = COUNT1 + 1
15900       END IF
15910     ELSE IF TEMP2RE(K) <> 0 OR TEMP2IM(K) <> 0 THEN
15920       LET FLAG = 1
15930       LET COUNT2 = MAX(COUNT1, COUNT2)
15940       LET COUNT1 = 0
15950     END IF
15960     IF MOD(K,128) = 0 THEN PRINT ".";
15970   NEXT K
15980   LET COUNT3 = COUNT3 + COUNT1
15990   LET COUNT2 = MAX(COUNT2, COUNT3)
16000   LET QNUM = N - COUNT2
16010   PRINT !TO RELEASE THE ";"
16020
16030   IF PNUM + QNUM > N + 1 THEN
16040     CLEAR
16050
16060     CALL BOX25( PNUM, QNUM, N, ZT$, A$, CONCORFLAG )
16070
16080     IF ZT$ = "Q" THEN LET MMENU = 1
16090   END IF
16100 END IF
16110
16120 IF MMENU = 0 THEN
16130   LET TIMESTART = TIME
16140   IF FORM$ = "POWEROF2" THEN
16150     CALL RUNFFT1( N, NU, XRE(), XIM(), X2RE(), X2IM(), TEMPRE(), TEMPIM(), FFTRE(), FFTIM(), FFT2RE(), FFT2IM(), KOS(), ZIN(), IBR, XFLAG, X2FLAG, REALFLAGX, FFTFLAG, FFT2FLAG, SYMMFLAG )
16160   ELSE IF FORM$ = "COMPOSITE" OR FORM$ = "PRIME" THEN
16170     CALL RUNFFT2( N, Q, R(), TEMPR(), MATK(,), DITK(), TEMPDITK(), XRE(), XIM(), X2RE(), X2IM(), TEMPRE(), TEMPIM(), TEMP2RE(), TEMP2IM(), FFTRE(), FFTIM(), FFT2RE(), FFT2IM(), KOS(), ZIN(), XFLAG, X2FLAG, REALFLAGX, FFTFLAG, FFT2FLAG, SYMMFLAG, FORM$ )
16180   END IF
16190   LET FFTFLAG = 1
16200   ! FFT OF PRIMARY  X  IS IN PRIMARY
16210
16220   FOR K = 0 TO N - 1
16230     LET FFT2RE(K) = FFTRE(K)         ! PUT PRIMARY FFT IN SECONDARY
16240     LET FFT2IM(K) = FFTIM(K)
16250   NEXT K
16260
16270   FOR K = 0 TO N - 1
16280     LET TEMPRE(K) = XRE(K)           ! PUT PRIMARY X IN SECONDARY
16290     LET TEMPIM(K) = XIM(K)           ! AND SECONDARY X IN PRIMARY
16300     LET XRE(K)    = X2RE(K)
16310     LET XIM(K)    = X2IM(K)
16320     LET X2RE(K)   = TEMPRE(K)
16330     LET X2IM(K)   = TEMPIM(K)
16340   NEXT K
16350
16360   ! NOW RUNNING FFT ON SECONDARY  X
16370   IF FORM$ = "POWEROF2" THEN
16380     CALL RUNFFT1( N, NU, XRE(), XIM(), X2RE(), X2IM(), TEMPRE(), TEMPIM(), FFTRE(), FFTIM(), FFT2RE(), FFT2IM(), KOS(), ZIN(), IBR, XFLAG, X2FLAG, REALFLAGX, FFTFLAG, FFT2FLAG, SYMMFLAG )
16390   ELSE IF FORM$ = "COMPOSITE" OR FORM$ = "PRIME" THEN
16400     CALL RUNFFT2( N, Q, R(), TEMPR(), MATK(,), DITK(), TEMPDITK(), XRE(), XIM(), X2RE(), X2IM(), TEMPRE(), TEMPIM(), TEMP2RE(), TEMP2IM(), FFTRE(), FFTIM(), FFT2RE(), FFT2IM(), KOS(), ZIN(), XFLAG, X2FLAG, REALFLAGX, FFTFLAG, FFT2FLAG, SYMMFLAG, FORM$ )
16410   END IF
16420   LET FFT2FLAG = 1
16430   ! FFT OF SECONDARY  X  IS NOW IN PRIMARY
16440
16450   FOR K = 0 TO N - 1            ! PUT PRIMARY X BACK INTO PRIMARY
16460     LET TEMPRE(K) = XRE(K)      ! AND SECONDARY X INTO SECONDARY
16470     LET TEMPIM(K) = XIM(K)
16480     LET XRE(K)    = X2RE(K)
16490     LET XIM(K)    = X2IM(K)
16500     LET X2RE(K)   = TEMPRE(K)
16510     LET X2IM(K)   = TEMPIM(K)
16520   NEXT K
16530
16540   FOR K = 0 TO N - 1
16550     LET TEMPRE(K) = FFTRE(K)     ! PUT PRIMARY FFT IN PRIMARY
16560     LET TEMPIM(K) = FFTIM(K)     ! AND SECONDARY IN SECONDARY
16570     LET FFTRE(K)  = FFT2RE(K)
16580     LET FFTIM(K)  = FFT2IM(K)
16590     LET FFT2RE(K) = TEMPRE(K)
16600     LET FFT2IM(K) = TEMPIM(K)
16610   NEXT K
16620
16630   IF CONCORFLAG = 0 THEN         ! IF RUNNING CORRELATION
16640     FOR K = 0 TO N - 1
16650       LET FFTIM(K) = -FFTIM(K)
16660     NEXT K
16670   END IF
16680
16690               ! PERFORM COMPLEX MULTIPLICATION OF PRIMARY AND SECONDARY FFT'S
16700   FOR K = 0 TO N - 1
16710     LET TEMPRE(K) = (FFTRE(K)*FFT2RE(K) - FFTIM(K)*FFT2IM(K))
16720     LET TEMPIM(K) = (FFTIM(K)*FFT2RE(K) + FFTRE(K)*FFT2IM(K))
16730   NEXT K
16740   FOR K = 0 TO N - 1
16750     LET FFTRE(K) = TEMPRE(K)
16760     LET FFTIM(K) = TEMPIM(K)
16770   NEXT K
16780
16790                       ! INVERT PRIMARY FFT TO OBTAIN IFFT
16800   IF FORM$ = "POWEROF2" THEN
16810     CALL RUNIFFT1( N, NU, FFTRE(), FFTIM(), FFT2RE(), FFT2IM(), TEMPRE(), TEMPIM(), TEMP2RE(), TEMP2IM, IFFTRE(), IFFTIM(), KOS(), ZIN(), IBR, FFTFLAG, FFT2FLAG, SYMMFLAG, IFFTFLAG, REALFLAGIFFT, CONVFLAG )
16820   ELSE IF FORM$ = "COMPOSITE" OR FORM$ = "PRIME" THEN
16830     CALL RUNIFFT2( N, Q, R(), TEMPR(), MATK(,), DITK(), TEMPDITK(), FFTRE(), FFTIM(), FFT2RE(), FFT2IM(), TEMPRE(), TEMPIM(), TEMP2RE(), TEMP2IM(), IFFTRE(), IFFTIM(), KOS(), ZIN(), FFTFLAG, FFT2FLAG, SYMMFLAG, IFFTFLAG, REALFLAGIFFT, FORM$, CONVFLAG )
16840   END IF
16850
16860   LET CONVFLAG = 1
16870
16880   LET TIMEEND = TIME
16890   LET TOTALTIME = TIMEEND - TIMESTART
16900
16910   CLEAR
16920
16930   CALL BOX57A( TOTALTIME, A$, CONCORFLAG )
16940
16950   CLEAR
16960
16970   CALL TESTFFTDATA( N, FFTRE(), FFTIM(), FFT2RE(), FFT2IM(), TEMP2RE(), TEMP2IM(), FFTFLAG, FFT2FLAG, SYMMFLAG, NOCONV, HJWFLAG, KLEEN$ )
16980   CALL TESTIFFTDATA( N, IFFTRE(), IFFTIM(), TEMP2RE(), TEMP2IM(), IFFTFLAG, REALFLAGIFFT, CONVFLAG, KLEEN$ )
16990
17000 END IF
17010
17020 END SUB !CONVOLUTION
17030 !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
